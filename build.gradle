plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id 'com.gradleup.shadow' version '9.3.0'
    id 'maven-publish'
    id 'net.saliman.properties' version '1.6.0'
}

group = 'one.armelin'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

configurations {
    create("shade")
    implementation {
        extendsFrom(configurations["shade"])
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(files("${project.HytaleInstallDir}/install/release/package/game/latest/Server/HytaleServer.jar"))
    implementation 'blue.endless:jankson:1.2.3'
    shade 'blue.endless:jankson:1.2.3'
}

def serverRunDir = file("$projectDir/run/server")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --assets=${project.HytaleInstallDir}/install/release/package/game/latest/Assets.zip"
        if (includes_pack.toBoolean()) {
            programParameters += " --mods=${sourceSets.main.java.srcDirs.first().parentFile.absolutePath}"
        }
        if (load_user_mods.toBoolean()) {
            programParameters += " --mods=${project.HytaleInstallDir}/UserData/Mods"
        }
        workingDirectory = serverRunDir.absolutePath
    }
}

tasks.shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.shade]
    mergeServiceFiles()
}